  
CREATE PROCEDURE [dbo].[USP_ATUALIZA_CLIENTES_CONSOLIDADO]  
  
AS  
  
  
IF OBJECT_ID('TBL_CLIENTES') IS NULL  
BEGIN  
  
--CRIANDO TABELA DE CLIENTES CONSOLIDADO  
--APENAS SE ELA NÃO EXISTIR  
CREATE TABLE TBL_CLIENTES  
(   
  ENTIDADE          NUMERIC(15) PRIMARY KEY  
, NOME              VARCHAR(80)  
, NOME_FANTASIA     VARCHAR(60)  
, INSCRICAO_FEDERAL VARCHAR(19)  
, CLASSIFICACAO     VARCHAR(80)  
, CIDADE            VARCHAR(80)  
, ESTADO            VARCHAR(80)  
, UF                CHAR(2)  
)  
  
END  
  
  
IF OBJECT_ID('TEMPDB..#DADOS_INTEGRACAO') IS NOT NULL DROP TABLE #DADOS_INTEGRACAO  
  
   SELECT A.ENTIDADE          AS ENTIDADE  
        , A.NOME              AS NOME  
        , A.NOME_FANTASIA     AS NOME_FANTASIA  
        , A.INSCRICAO_FEDERAL AS INSCRICAO_FEDERAL  
        , B.DESCRICAO         AS CLASSIF_CLIENTE  
        , C.CIDADE            AS CIDADE  
        , D.NOME              AS ESTADO  
        , C.ESTADO            AS UF  
     INTO #DADOS_INTEGRACAO  
     FROM ENTIDADES               A  
LEFT JOIN CLASSIFICACOES_CLIENTES B ON A.CLASSIFICACAO_CLIENTE = B.CLASSIFICACAO_CLIENTE  
LEFT JOIN ENDERECOS               C ON A.ENTIDADE              = C.ENTIDADE  
LEFT JOIN ESTADOS                 D ON C.ESTADO                = D.ESTADO  
  
--POPULANDO TABELA DE CLIENTES CONSOLIDADO  
MERGE TBL_CLIENTES       D  
USING #DADOS_INTEGRACAO  O ON D.ENTIDADE = O.ENTIDADE  
WHEN MATCHED THEN UPDATE  
SET NOME              = O.NOME  
  , NOME_FANTASIA     = O.NOME_FANTASIA  
  , INSCRICAO_FEDERAL = O.INSCRICAO_FEDERAL  
  , CLASSIFICACAO     = O.CLASSIF_CLIENTE  
  , CIDADE            = O.CIDADE  
  , ESTADO            = O.ESTADO  
  , UF                = O.UF  
  
  
WHEN NOT MATCHED BY TARGET THEN INSERT  
(   ENTIDADE           
  , NOME               
  , NOME_FANTASIA      
  , INSCRICAO_FEDERAL  
  , CLASSIFICACAO      
  , CIDADE             
  , ESTADO             
  , UF )  
    
VALUES(  
 O.ENTIDADE  
,O.NOME  
,O.NOME_FANTASIA  
,O.INSCRICAO_FEDERAL  
,O.CLASSIF_CLIENTE  
,O.CIDADE  
,O.ESTADO  
,O.UF ) ;  
  